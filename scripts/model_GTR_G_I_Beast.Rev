################################################################################
#
# RevBayes Script: Inferring a Phylogeny under GTR+Gamma+Inv model
# 
#
# authors:  Sebastian HÃ¶hna and Luiza Fabreti
#
################################################################################

######################
# Substitution Model #
######################

# specify the stationary frequency parameters
pi_prior <- v(1,1,1,1) 
pi ~ dnDirichlet(pi_prior)
moves.append( mvBetaSimplex(pi, weight=4.0) )
moves.append( mvDirichletSimplex(pi, weight=2.0) )


# specify the exchangeability rate parameters
er_param[1] ~ dnGamma(0.05, 1/10.0)
er_param[2] ~ dnGamma(0.05, 1/20.0)
er_param[3] ~ dnGamma(0.05, 1/10.0)
er_param[4] ~ dnGamma(0.05, 1/10.0)
er_param[5] <- 1.0
er_param[6] ~ dnGamma(0.05, 1/10.0)
er := simplex( er_param )

moves.append( mvScale(er_param[1], weight=2.0) )
moves.append( mvScale(er_param[2], weight=2.0) )
moves.append( mvScale(er_param[3], weight=2.0) )
moves.append( mvScale(er_param[4], weight=2.0) )
moves.append( mvScale(er_param[6], weight=2.0) )


# create a deterministic variable for the rate matrix, GTR
Q := fnGTR(er,pi) 


#############################
# Among Site Rate Variation #
#############################

alpha ~ dnExponential(1.0)
sr := fnDiscretizeGamma( alpha, alpha, 4, false )
moves.append( mvScale(alpha, lambda=1.0, weight=4.0) )


# the probability of a site being invariable, +I
p_inv ~ dnUniform(0.0,1.0)
moves.append( mvBetaProbability(p_inv, weight=4.0) )


